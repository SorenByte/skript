options:
    spawntime: 10 #amount of time it takes to teleport to spawn in seconds.
    cooldown: &cYou are trying to use this command too fast! &7(Try again in %remaining time%) #cooldown messages for commands.
    error: &4<&c!&4>
    renamelength: 32 #the maximum length for renaming items
    nicknamelength: 24 #the maximum length for player nicknames

# Enderchest related stuff.

command /enderchest [<offline player>]:
    aliases: ec
    permission: skript.enderchest
    trigger:
        if arg-1 is not set:
            open player's enderchest for player
            play sound "block.ender_chest.open" with pitch 2 for player
            send "&a◆ &7Opened &aYour&7 enderchest."
        else:
            if player has permission "staff":
                open arg-1's enderchest for player
                play sound "block.ender_chest.open" with pitch 2 for player
                send "&a◆ &7Opened &a%arg-1%'s&7 enderchest."
on tab complete of "/enderchest":
    if player does not have permission "staff":
        set tab completions for position 1 to ""

# Gamemode related stuff.

command /gmc [<player>]:
    permission: op
    trigger:
        if arg-1 is not set:
            set gamemode of player to creative
        else:
            set gamemode of arg-1 to creative
            send "&d◆ &7Set gamemode of &b%arg-1%&7 to &dcreative&7." to player
command /gms [<player>]:
    permission: op
    trigger:
        if arg-1 is not set:
            set gamemode of player to survival
        else:
            set gamemode of arg-1 to survival
            send "&a◆ &7Set gamemode of &b%arg-1%&7 to &asurvival&7." to player
command /gma [<player>]:
    permission: op
    trigger:
        if arg-1 is not set:
            set gamemode of player to adventure
        else:
            set gamemode of arg-1 to adventure
            send "&e◆ &7Set gamemode of &b%arg-1%&7 to &eadventure&7." to player
command /gmsp [<player>]:
    permission: op
    trigger:
        if arg-1 is not set:
            set gamemode of player to spectator
        else:
            set gamemode of arg-1 to spectator
            send "&9◆ &7Set gamemode of &b%arg-1%&7 to &9spectator&7." to player
on gamemode change:
    if metadata value "isChanging" of player is not set:
        set metadata value "isChanging" of player to 1
        cancel event
        if gamemode of player is creative:
            send "&d◆ &7Your gamemode has been set to &dcreative&7."
        if gamemode of player is survival:
            send "&a◆ &7Your gamemode has been set to &asurvival&7."
        if gamemode of player is adventure:
            send "&e◆ &7Your gamemode has been set to &eadventure&7."
        if gamemode of player is spectator:
            send "&9◆ &7Your gamemode has been set to &9spectator&7."
        set gamemode of player to gamemode of player
        delete metadata value "isChanging" of player
        stop

#Flyspeed

command /fly:
    permission: skript.fly
    trigger:
        if player is not flying:
            push player upwards with speed 0.1
            send "&a◆ &7Your fly has been &aenabled&7." to player
            wait 1 tick
            make player fly
        else:
            if player is flying:
                send "&c◇ &7Your fly has been &cdisabled&7." to player
                make player stop flying
command /flyspeed [<integer>]:
    aliases: fs
    permission: op
    trigger:
        if arg-1 is set:
            set player's flying speed to ((arg-1) / 10)
            send "&a◆ &7Your flying speed has been set to &a%arg-1%&7." to player
        else:
            set player's flying speed to 0.1
            send "&c◇ &7Your flying speed has been &creset&7." to player

# Feed

command /feed:
	cooldown: 1 minute
	cooldown message: {@cooldown}
	cooldown bypass: op
	permission: skript.feed
	trigger:
		set player's hunger to 20
		send "&aYour hunger has been satiated"

# Hat

command /hat:
    permission: skript.hat
    trigger:
        if player's helmet is air:
            if player's held item is not air:
                if display name of player's held item is set:
                    send "&a◆ &7Equipped &a%display name of player's held item%&7 as a hat."
                    set player's helmet to player's held item
                    set player's held item to air
                else:
                    set {_type} to "%type of player's held item%"
                    send "&a◆ &7Equipped &a%capitalizeSentence({_type})%&7 as a hat."
                    set player's helmet to player's held item
                    set player's held item to air
            else:
                send "{@error} &cHold an item to use &4/Hat&c."
        else:
            send "{@error} &cRemove your helmet to use &4/Hat&c."

# Nickname

command /nickname [<string>]:
    aliases: nick
    permission: skript.nick
    trigger:
        if arg-1 is set:
            if player is op:
                set player's nickname to colored arg-1
                send "&a◆ &7Set your nickname to &a%colored arg-1%&7." to player
            else:
                if length of arg-1 < {@nicknamelength}:
                    if colored arg-1 is alphanumeric:
                        set player's nickname to colored arg-1
                        send "&a◆ &7Set your nickname to &a%colored arg-1%&7." to player
                    else:
                        send "{@error} &cYour nickname must be alphanumeric! &7(Example: Soren99)" to player
                else:
                    send "{@error} &cThe length of your nickname cannot be greater than &4{@nicknamelength}&c!" to player
        else:
            reset player's nickname
            send "&c◇ &7You have &cCleared&7 your nickname."

# Item Rename

command /rename [<text>]:
    permission: skript.rename
    trigger:
        if player is op:
            set display name of player's tool to colored arg-1
        else:
            if length of arg-1 is less than {@renamelength}:
                set display name of player's tool to colored arg-1
            else:
                send "{@error} &cThe length of your item name cannot be greater than &4{@renamelength}&c!" to player
on tab complete of "/rename":
	set tab completions for position 1 to colored display name of player's tool
	replace "§" in tab completions with "&"

# Craft

command /craft:
    cooldown message: {@cooldown}
    cooldown: 10 seconds
    cooldown bypass: op
    permission: skript.craftingtable
    trigger:
        open crafting table for player
        send "&a◆ &7Portable crafting table &aopened&7."

command /repair:
    cooldown message: {@cooldown}
    cooldown: 1 seconds
    cooldown bypass: op
    permission: skript.repair
    trigger:
        repair held item of player
        send "&a◆ &7Your held item has been &arepaired&7."

command /walkspeed [<integer>]:
    cooldown message: {@cooldown}
    cooldown: 1 seconds
    cooldown bypass: op
    permission: skript.walkspeed
    trigger:
        if arg-1 is set:
            set player's walking speed to ((arg-1) / 10)
            send "&a◆ &7Your walking speed has been set to &a%arg-1%&7." to player
        else:
            set player's walking speed to 0.2
            send "&c◇ &7Your walking speed has been &creset&7." to player

command /spawnmob [<entitytype>] [<integer>]:
    cooldown message: {@cooldown}
    cooldown: 1 seconds
    cooldown bypass: op
    permission: skript.spawnmob
    trigger:
        if arg-1 and arg-2 are set:
            spawn arg-2 of arg-1 at player
        else:
            send "&c◇ Invalid arguments! &7(/spawnmob <entity> <amount>)"

command /tpall:
    cooldown message: {@cooldown}
    cooldown: 1 seconds
    cooldown bypass: op
    permission: skript.tpall
    trigger:
        teleport all players to player
        broadcast "&a◆ &7All players have been teleported to &a%player%&7."

command /anvil:
    cooldown message: {@cooldown}
    cooldown: 1 seconds
    cooldown bypass: op
    permission: skript.anvil
    trigger:
        open anvil to player
        send "&a◆ &7Portable anvil &aopened&7." to player

command /grindstone:
    cooldown message: {@cooldown}
    cooldown: 1 seconds
    cooldown bypass: op
    permission: skript.grindstone
    trigger:
        open grindstone inventory to player
        send "&a◆ &7Portable grindstone &aopened&7." to player

command /loom:
    cooldown message: {@cooldown}
    cooldown: 1 seconds
    cooldown bypass: op
    permission: skript.loom
    trigger:
        open loom inventory to player
        send "&a◆ &7Portable loom &aopened&7." to player

command /smithingtable:
    cooldown message: {@cooldown}
    cooldown: 1 seconds
    cooldown bypass: op
    permission: skript.smithingtable
    trigger:
        open smithing inventory to player
        send "&a◆ &7Portable smithing table &aopened&7." to player

command /stonecutter:
    cooldown message: {@cooldown}
    cooldown: 1 seconds
    cooldown bypass: op
    permission: skript.stonecutter
    trigger:
        open stonecutter inventory to player
        send "&a◆ &7Portable stonecutter &aopened&7." to player

command /broadcast [<text>]:
    cooldown message: {@cooldown}
    cooldown: 1 seconds
    cooldown bypass: op
    permission: skript.broadcast
    trigger:
        if arg-1 is set:
            sendBarText(all players, "&4", "&c&lBROADCAST &8→ &7%colored arg-1%")
        else:
            send "&c◇ Invalid arguments! &7(/broadcast <message>)"

command /sudo [<player>] [<text>]:
    cooldown message: {@cooldown}
    cooldown: 1 seconds
    cooldown bypass: op
    permission: skript.sudo
    trigger:
        if arg-1 and arg-2 are set:
            make arg-1 say arg-2
        else:
            send "&c◇ Invalid arguments! &7(/sudo <player> <message/command>)"

command /skull [<offline player>]:
    cooldown message: {@cooldown}
    cooldown: 1 seconds
    cooldown bypass: op
    permission: skript.skull
    trigger:
        if arg-1 is set:
            give player ("%arg-1%" parsed as offline player)'s skull
            send "&a◆ &7You have been given the skull of &a%arg-1%&7." to player
        else:
            send "&c◇ Invalid arguments! &7(/skull <player>)"

command /tphere [<player>]:
    cooldown message: {@cooldown}
    cooldown: 1 seconds
    cooldown bypass: op
    permission: skript.tphere
    trigger:
        if arg-1 is set:
            teleport arg-1 to player
            send "&a◆ &7You have teleported &a%arg-1%&7 to you." to player
            send "&a◆ &a%player%&7 has teleported you to them." to arg-1
        else:
            send "&c◇ Invalid arguments! &7(/tphere <player>)"
